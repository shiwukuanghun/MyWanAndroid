import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

//获取系统时间
def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd_HH-mm-ss")
    df.setTimeZone(TimeZone.getDefault())
    return df.format(new Date())
}

android {
    signingConfigs {
        debug {
            keyAlias 'mywan'
            keyPassword '123456'
            storeFile file('mywan.jks')
            storePassword '123456'
        }
        release {
            keyAlias 'mywan'
            keyPassword '123456'
            storeFile file('mywan.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.wujie.mywanandroid"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        multiDexEnabled = true
    }

    dexOptions{
        javaMaxHeapSize "2g"
        jumboMode = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //解决appcompat版本不一致
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '28.0.0'
                }
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def version = "v${versionName}-${versionCode}"
                def fileName = ""
                if (variant.buildType.name == "release") {
//                    fileName = "MyWanAndroid_${version}_${buildTime()}_Release.apk"
                    fileName = "MyWanAndroid_${version}}_Release.apk"
                } else if (variant.buildType.name == "debug") {
//                    fileName = "MyWanAndroid_${version}_${buildTime()}_Debug.apk"
                    fileName = "MyWanAndroid_${version}}_Debug.apk"
                }
                outputFileName = fileName

            }
        }
    }

    //过滤掉提示重复的文件,加了rxBinding后出现的，出现类似问题照此办
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
    implementation project(':commonmoudle')
}


